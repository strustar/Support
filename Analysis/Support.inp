! 2023-08-16, Support 3D Detailed Analysis
! Units : N, mm

FINISH
/CLEAR

joist_b = 50.0  $  joist_h = 50  $  joist_t = 2.3  $  Lj = 150.0
yoke_b = 75   $  yoke_h = 125  $  yoke_t = 3.2   $  Ly = 914
vertical_d = 60.5  $  vertical_t = 2.6  $  Lv = 914
horizontal_d = 42.7  $  horizontal_t = 2.2  $  Lh = 1725
bracing_d = 42.7  $  bracing_t = 2.2

slab_X = 8000  $  slab_Y = 12000  $  height = 9500

dead_load = 10  $  design_load = 12.5  $  Hx = 0.2  $  Hy = 0.2  $  wind = 0.3  ! kN/m2

/sys, del/q/f *.wmf  ! wmf 모든 파일 지우기
!! ===============================================> Preprocessing
/PREP7	
	!/uis,msgpop,3  !!  3 : Only errors displayed.
	! x : 수직재 개수, 간격
	!!! Modelling
	xea = nint(slab_X/Lv) + 1 ! Nearest integer to x
	*if,mod(slab_X, Lv),lt,Lv/2,then
		xea = xea + 1
	*endif
	
	! y : 멍에 개수, 간격
	yea = nint(slab_Y/Ly) + 1 ! Nearest integer to x
	*if,mod(slab_Y, Ly),lt,Ly/2,then
		yea = yea + 1
	*endif

	! z : 수평재 간격
	zea = nint(height/Lh) + 1 ! Nearest integer to x
	*if,mod(height, Lh),lt,Lh/2,then
		zea = zea + 1
	*endif

	k,1, 0,0,0
	kgen,xea, 1,1,1,  Lv,0,0, 1
	kgen,yea, all,,, 0,Ly,0, 100
	kgen,zea, all,,, 0,0,Lh, 10000

	*do,i,1,xea  !! 수직재
		*do,j,1,yea
			*do,k,1,zea-1
				l, i + 100*(j-1) + 10000*(k-1), i + 10000 + 100*(j-1) + 10000*(k-1)
			*enddo
		*enddo
	*enddo
	alls  $  cm, ver, line  $  /color,line,magenta

	*do,i,1,xea-1  !! 수평재
		*do,j,1,yea
			*do,k,1,zea-1
				l, i + 10000 + 100*(j-1) + 10000*(k-1), i + 10001 + 100*(j-1) + 10000*(k-1)
			*enddo
		*enddo
	*enddo

	*do,i,1,xea  !! 수평재
		*do,j,1,yea-1
			*do,k,1,zea-1
				l, i + 10000 + 100*(j-1) + 10000*(k-1), i + 10100 + 100*(j-1) + 10000*(k-1)
			*enddo
		*enddo
	*enddo
	alls  $  cmsel,u,ver  $  cm, hor, line  $  /color,line,cyan

	*do,i,1,xea-1,2  !! 가새재
		*do,j,1,yea,2
			*do,k,1,zea-1,2
				l, i + 100*(j-1) + 10000*(k-1), i + 10001 + 100*(j-1) + 10000*(k-1)
			*enddo
		*enddo
	*enddo
	alls  $  cmsel,u,ver  $  cmsel,u,hor  $  cm, bra, line  $  /color,line,orange

	allsel  $  NUMMRG,all
/VUP,,z
/VIEW,,1,-1,1
! ! alls  $  /rep
! /delete,tt000,wmf
lplot
/ui,copy,save,wmf
!!! Modelling
	
!!! Attributes & Meshing
	ET,1,BEAM188

	MP,EX,1, 200e3
	MP,PRXY,1, 0.3	
	! mp,dens,1, 2.35e-6 ! kg / mm^3

	SECTYPE,1, BEAM, CTUBE  $  SECDATA, vertical_d/2 - vertical_t, vertical_d/2  ! Ri, Ro, N
	SECTYPE,2, BEAM, CTUBE  $  SECDATA, horizontal_d/2 - horizontal_t, horizontal_d/2  ! Ri, Ro, N
	SECTYPE,3, BEAM, CTUBE  $  SECDATA, bracing_d/2 - bracing_t, bracing_d/2  ! Ri, Ro, N

	cmsel,s,ver  $  LATT, 1,,1,,,, 1
	cmsel,s,hor  $  LATT, 1,,1,,,, 2
	cmsel,s,bra  $  LATT, 1,,1,,,, 3

	/rep
	alls  $  lesize,all, 200	
	LMESH,all

	ESEL,s,sec,, 1  $  cm, v, elem  $  /color,elem,magenta
	ESEL,s,sec,, 2  $  cm, h, elem  $  /color,elem,cyan
	ESEL,s,sec,, 3  $  cm, b, elem  $  /color,elem,orange

	alls  $  NUMMRG,all
	ALLSEL,all
	! *return
!!! Attributes & Meshing
FINISH
!! ===============================================> Preprocessing

!! ===============================================> Solution
/SOLU
	nsel,s,loc, z,0
	d,all,all,0

	ALLSEL,all
	cmsel,s,v
	nsle,s
	nsel,r,loc, z, Lh*(zea-1)
	
	f,all, fz, -design_load*1e3*Ly*Lv/1e6
	eplot
	alls
	/pbc,f,,1
! *return
	ALLSEL,all
	solve
FINISH
!! ===============================================> Solution

!*return
!! ===============================================> Postprocessing
/POST1
SET,LAST
!/esh,1
!/dscale,,1
plns,u,sum
!plns,s,eqv

! *RETURN,
	ETABLE,Fx1, SMISC,1  !!ETABLE, Lab, Item, Comp, Option
	ETABLE,Fx2, SMISC,14
		
	PLLS,Fx1,Fx2  !!PLLS, LabI, LabJ, Fact, KUND,ViewUP 	
	! pletab,Fx1
	! cmsel,s,ebar
	! /rep
	! /ui,copy,save,wmf


